
apply plugin: 'java-library'
apply plugin: 'maven-publish'

dependencies {
    api deps.styled_text_api
    api deps.tcommon_core
    
    implementation deps.guava
    implementation deps.tcommon_logging
    implementation deps.tcommon_prefsstore
    
    testImplementation deps.tcommon_test
}

publishing {
    publications {
        api(MavenPublication) {
            from components.java
            artifactId 'nvlist-api'
            pom.withXml {
                asNode().children().last() + pomLicenseConfig
            }  
        }
    }
}

bintray {
    publications = ['api']
}

configurations {
    oldApi {
        transitive = false
    }
}

dependencies {
    oldApi "nl.weeaboo.vn:nvlist-api:4.0.0-alpha10"
}

task japicmp(type: me.champeau.gradle.japicmp.JapicmpTask) {
    group = 'verification'

    oldClasspath = files(configurations.oldApi)
    newClasspath = files(jar)
    ignoreMissingClasses = true
    onlyModified = true
    failOnModification = true
    htmlOutputFile  = file("$buildDir/reports/japi.html")
}

javadoc {
    options {
        noDeprecatedList = true
        noHelp = true
        noIndex = true
        noTimestamp = true

        // Don't include the version number (causes too many unrelated changes)
        title = 'NVList'
    }
}
