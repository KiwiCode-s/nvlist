
// Loads a .properties file
def loadProperties(String filename) {
    def props = new Properties()
    def propertiesFile = file(filename)
    if (propertiesFile.canRead()) {
        propertiesFile.withInputStream { instr ->
            props.load(instr)
        }
    }
    return props
}

if (!hasProperty('vnRoot')) {
    gradle.ext.vnRoot = '..'
}
logger.lifecycle("VN root parameter: " + gradle.ext.vnRoot)

def x = gradle.ext
x.vnRootDir = new File(gradle.ext.vnRoot).getCanonicalFile()
x.vnResDir = new File(x.vnRootDir, "/res")
x.buildResDir = new File(x.vnRootDir, "/build-res")
x.buildToolsDir = rootProject.projectDir

// Load user-defined build.properties file
x.settings = loadProperties("${x.buildResDir}/build.properties")
rootProject.name = x.settings.getProperty("appShortName", "NVList-project")

logger.lifecycle("VN resource folder: " + x.vnResDir)
logger.lifecycle("VN build-resource folder: " + x.buildResDir)

include "desktop"
include "android"
include "ios"

// Uncomment the following line to compile using a local NVList build instead of a released version
// includeBuild "../.."