
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        // Plugin for making a 'fat' JAR
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'

        // Plugins for Android
        classpath 'com.android.tools.build:gradle:2.2.2'        
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'        

        // Plugins for iOS
        classpath "com.mobidevelop.robovm:robovm-gradle-plugin:2.3.0"
    }
}

plugins {
    // Plugin for uploading to bintray
    // Note: Causes a classpath conflict for org.apache.tools.zip
    id 'com.jfrog.bintray' version '1.7.3'
    
    id "net.ltgt.errorprone" version "0.0.8"
    id "com.github.hierynomus.license" version "0.13.1"
}

def templateDir = file('template').getAbsoluteFile()

allprojects {
    apply plugin: 'eclipse'
    apply plugin: 'jacoco'
    apply plugin: 'com.jfrog.bintray'

    group = 'nl.weeaboo.vn'
    version = '4.0.0-alpha10'
    ext {
        javaVersion = JavaVersion.VERSION_1_8
        
        gdxTestVersion = '1.0.4'
        gdxVersion = '1.9.5'
        gdxVideoVersion = '0.0.4-anonl'
        guavaVersion = '19.0'        
        junitVersion = '4.12'
        luajpp2Version = '2.3.14'
        roboVmVersion = '2.3.0'
        slf4jVersion = '1.7.21'
        styledTextVersion = '1.5.4'
        tcommonVersion = '1.2.5'
        
        vnRootDir = templateDir
        vnResDir = new File(vnRootDir, 'res')
        buildToolsDir = new File(templateDir, 'build-tools')        
    }

    repositories {
        jcenter()
        maven { url 'https://dl.bintray.com/anonl/gdx/' }
        maven { url 'https://dl.bintray.com/anonl/gdx-styledtext/' }
        maven { url 'https://dl.bintray.com/anonl/gdx-video/' }
        maven { url 'https://dl.bintray.com/anonl/luajpp2/' }
        maven { url 'https://dl.bintray.com/anonl/tcommon/' }
    }
        
    plugins.withType(JavaPlugin) {    
        sourceCompatibility = javaVersion
        
        tasks.withType(JavaCompile) {
            options.encoding = 'UTF-8'
        }
                
        dependencies {
            testCompile "junit:junit:$junitVersion"
            testCompile "com.google.guava:guava-testlib:$guavaVersion"
            
            // gdx-test is a required dependency since it contains the GdxUiTest test category
            testCompile "nl.weeaboo.gdx-test:gdx-test-core:$gdxTestVersion"
        }
        
        test {
            def uiTest = project.hasProperty('uiTest')
            inputs.property('uiTest', uiTest) // Re-execute task if uiTest property changes        
            useJUnit {
                if (!uiTest) {            
                    // Exclude UI tests by default
                    excludeCategories 'nl.weeaboo.gdx.test.junit.GdxUiTest'
                }
            }
        }          

        jacocoTestReport {
            reports {
                xml.enabled true
            }
        }    
    }
    
    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')
        pkg {
            repo = 'nvlist'
            name = 'nvlist'
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/anonl/nvlist.git'
            version {
                name = project.version
            }
        }
    }    
}

def applyErrorPronePlugin(project) {
    project.apply plugin: 'net.ltgt.errorprone'

    project.configurations.errorprone {
        resolutionStrategy.force 'com.google.errorprone:error_prone_core:2.0.14'
    }
}
