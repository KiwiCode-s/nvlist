
buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/releases' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.0-alpha1'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.6"
}

allprojects {
    apply plugin: "eclipse"

    group = 'nl.weeaboo.vn'
    version = '4.0.0-alpha2'
    ext {
        appName = 'NVList'
        gdxVersion = '1.9.2'
        junitVersion = '4.12'
        slf4jVersion = '1.7.12'
        guavaVersion = '19.0'
        styledTextVersion = '1.3.1'
        luajpp2Version = '2.1.1'
        tcommonVersion = '1.0.0'
    }

    repositories {
        jcenter()
        maven { url "https://dl.bintray.com/anonl/gdx-styledtext/" }
        maven { url "https://dl.bintray.com/anonl/luajpp2/" }
        maven { url "https://dl.bintray.com/anonl/tcommon/" }
    }
    
    plugins.withType(JavaPlugin) {
        sourceCompatibility = 1.6
        targetCompatibility = 1.6
        compileJava.options.encoding = 'UTF-8'
        compileTestJava.options.encoding = 'UTF-8'
    }
}

project(':api') {
    apply plugin: 'java'
}

project(':core') {
    apply plugin: 'java'
}

project(':desktop') {
    apply plugin: 'java'
}

project(':android') {
    apply plugin: 'com.android.library'
    apply plugin: 'com.github.dcendents.android-maven'
}

apply plugin: 'maven-publish'

publishing {
    publications {
        desktop(MavenPublication) {
            from project(':desktop').components.java
            artifactId 'nvlist-desktop'
        }
        android(MavenPublication) {
            def androidProj = project(':android')
            artifact "${androidProj.buildDir}/outputs/aar/${androidProj.name}-release.aar"
            artifactId 'nvlist-android'
        }
    }
}

bintray {   
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['desktop', 'android']
    pkg {
        repo = 'nvlist'
        name = 'nvlist'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/anonl/nvlist.git'
        version {
            name = project.version
        }
    }
}
